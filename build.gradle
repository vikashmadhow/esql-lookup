/*
 * Copyright (c) 2021 Vikash Madhow
 */

/**
 * ESQL lookup functionality as an extension.
 *
 * @author Vikash Madhow (vikash.madhow@gmail.com)
 */

buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id "java-library"
  id "maven-publish"
  id "signing"
  id 'io.codearte.nexus-staging' version '0.22.0'
  id "synapticloop.projectFilestatistics" version "1.1.0"
}

repositories {
  mavenLocal()
  mavenCentral()
}

group = 'ma.vi'
version "1.0.1"

project.ext {
  moduleName = project.group + '.' + project.name
  isReleaseVersion = !version.endsWith("SNAPSHOT")
}

java {
//  sourceCompatibility = JavaVersion.VERSION_17
  withJavadocJar()
  withSourcesJar()
}

jar {
  inputs.property("moduleName", project.moduleName)
  manifest {
    attributes('Automatic-Module-Name': project.moduleName)
  }
}

dependencies {
  implementation("ma.vi:esql:latest.release")
//  implementation("ma.vi:esql-etl:latest.release")
//  implementation("ma.vi:esql-compute:latest.release")

  // Use JUnit test framework
  testImplementation("org.junit.jupiter:junit-jupiter:5.9.2")
  testRuntimeOnly("org.junit.platform:junit-platform-launcher")
  testImplementation('org.apache.commons:commons-lang3:3.12.0')
}

test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}

test.dependsOn(jar)

publishing {
  repositories {
    maven {
      def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
      url = isReleaseVersion ? releaseRepo : snapshotRepo
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        groupId = 'ma.vi'
        artifactId = 'esql-lookup'
        name = "ESQL lookup functionality as an extension"
        description = 'ESQL lookup functionality as an extension'
        url = 'https://github.com/vikashmadhow/esql-lookup'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = "vikashmadhow"
            name = "Vikash Madhow"
            email = "vikash.madhow@gmail.com"
            url = "http://vikmad.com"
          }
        }
        scm {
          url = 'https://github.com/vikashmadhow/esql-lookup'
          connection = 'scm:https://github.com/vikashmadhow/esql-lookup.git'
          developerConnection = 'scm:git://github.com/vikashmadhow/esql-lookup.git'
        }
      }
    }
  }
}

projectFilestatistics {
  // the files to include in the generation of the statistics
  includes = [
          "src/**/*.*",
  ]

  // the files to exclude from the generation
  excludes = [
          "**/node_modules/*",
          "**/*.woff*",
          "**/*.babelrc",
          "**/*.editorconfig",
          "**/*.eot",
          "**/*.eslintignore",
          "**/*.gif",
          "**/*.ico",
          "**/*.interp",
          "**/*.jpg",
          "**/*.json",
          "**/*.keep",
          "**/*.map",
          "**/*.orig",
          "**/*.otf",
          "**/*.png",
          "**/*.svg",
          "**/*.tokens",
          "**/*.ttf",
          "**/*.xls*",
          "**/*.pfx",
          "**/dist/*"
  ]

  // the output directory for the generated statistics
  outputDirectory = "$layout.buildDirectory/docs"

  // if you require to over-ride how single and multi-line comments work, or
  // need to add in some extensions which aren't built in...
  // propertyFile = 'over-ride.properties'
}

//signing {
//  sign publishing.publications.mavenJava
//}
//
//tasks.withType(Sign) {
//  onlyIf { isReleaseVersion }
//}
//
//nexusStaging {
//  packageGroup = group
//  username = project.getProperty('ossrhUsername')
//  password = project.getProperty('ossrhPassword')
//}